plugins {
	id 'org.quiltmc.loom' version '1.0.+'
	id 'maven-publish'
	id 'io.github.juuxel.loom-quiltflower' version '1.7.4'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	maven {
		name = 'Quilt'
		url = 'https://maven.quiltmc.org/repository/release'
	}
	maven {
		name = 'Quilt Snapshots'
		url = 'https://maven.quiltmc.org/repository/snapshot'
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	// mappings "org.quiltmc:quilt-mappings:${minecraft_version}+build.${project.quilt_mappings_build}:intermediary-v2"
	mappings loom.officialMojangMappings()
	modImplementation "org.quiltmc:quilt-loader:${project.loader_version}"
	compileClasspath "org.quiltmc:quilt-config:${project.quilt_config_version}"

	// Fabric API.
	// modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

quiltflower {
	addToRuntimeClasspath.set(true)
	quiltflowerVersion.set('1.9.0')
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		maven {
			url 's3://maven.bymartrixx.me'
			credentials(AwsCredentials) {
				accessKey = System.env.AWS_ACCESS_KEY ?: findProperty('mavenAccessKey')
				secretKey = System.env.AWS_SECRET_KEY ?: findProperty('mavenSecretKey')
			}
		}
	}
}
