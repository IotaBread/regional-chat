plugins {
	alias libs.plugins.quilt.loom
	alias libs.plugins.loom.quiltflower
	alias libs.plugins.minotaur
	alias libs.plugins.cursegradle
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	maven {
		name = 'Quilt'
		url = 'https://maven.quiltmc.org/repository/release'
	}
	maven {
		name = 'Quilt Snapshots'
		url = 'https://maven.quiltmc.org/repository/snapshot'
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft libs.minecraft
	// mappings variantOf(libs.quilt.mappings) { classifier("intermediary-v2") }
	mappings loom.officialMojangMappings()
	modImplementation libs.quilt.loader
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

quiltflower {
	addToRuntimeClasspath.set(true)
}

modrinth {
	token = project.hasProperty('modrinth_token') ? project.property('modrinth_token') : System.getenv('MODRINTH_TOKEN')
	projectId = 'B6Z2SJET'
	versionNumber = project.version
	versionName = "Regional Chat ${project.version} for Minecraft 1.20.1"
	uploadFile = remapJar
	changelog = "A changelog can be found at https://github.com/ByMartrixx/regional-chat/releases/tag/${version}"
	gameVersions = ['1.20.1']
	loaders = ['fabric', 'quilt']
}

curseforge {
	if (project.hasProperty('curse_api_key') || System.getenv('CURSE_API_KEY') != null) {
		apiKey = project.hasProperty('curse_api_key') ? project.property('curse_api_key') : System.getenv('CURSE_API_KEY')
	}
	project {
		id = '558251'
		changelog = "A changelog can be found at https://github.com/ByMartrixx/regional-chat/releases/tag/${version}"
		releaseType = 'release'
		addGameVersion '1.20.1'
		addGameVersion 'Fabric'
		addGameVersion 'Quilt'
		mainArtifact(remapJar) {
			displayName = "Regional Chat ${version} for Minecraft 1.20.1"
		}
		afterEvaluate {
			uploadTask.dependsOn('remapJar')
		}
	}
	options {
		forgeGradleIntegration = false
	}
}

publish.finalizedBy tasks.curseforge, tasks.modrinth

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		mavenLocal()
	}
}
